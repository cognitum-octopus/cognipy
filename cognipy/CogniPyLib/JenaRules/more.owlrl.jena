#------------------------------------------------------------
# More Inferencing Template Rules (NOT COMPLETELY TESTED)
#------------------------------------------------------------
[min: 
	(?R owl:minCardinality 1),
	(?R owl:onProperty ?P),
	(?X rdf:type ?R ), noValue(?X ?P ), makeTemp(?T) -> (?X ?P ?T)]

[minRec: 
	(?R owl:minCardinality 1),
	(?R owl:onProperty ?P),
	(?X ?P ?Y) -> (?X rdf:type ?R )]

[maxRec: 
	(?R owl:maxCardinality 1),
	(?R owl:onProperty ?P),
	(?P rdf:type owl:FunctionalProperty),
	(?X rdf:type owl:NamedIndividual ) -> (?X rdf:type ?R )]

[cardRec3: 
	(?R1 owl:cardinality 0),
	(?R1 owl:onProperty ?P),
	(?R2 owl:onProperty ?P), 
	(?R2 owl:maxCardinality 0),
	(?X rdf:type ?R2 )  -> (?X rdf:type ?R1 )]

[cardRec2: 
	(?R1 owl:cardinality 1),
	(?R1 owl:onProperty ?P),
	(?R2 owl:onProperty ?P),
	(?R2 owl:minCardinality 1),
	(?R3 owl:onProperty ?P),
	(?R3 owl:maxCardinality 1),
	(?X rdf:type ?R2 ), (?X rdf:type ?R3 ) -> (?X rdf:type ?R1 )]

# be aware of the recursions... it should be checked
[some1b_1b2: 
	(?R owl:someValuesFrom ?D),
	(?R owl:onProperty ?P), 
	(?X rdf:type ?R ), notBNode(?X), noValue(?X ?P ), makeTemp(?T) -> (?T rdf:type ?D ), (?X ?P ?T)]

[someRec2: 
	(?R owl:someValuesFrom ?D),
	(?R owl:onProperty ?P),
	(?P rdf:type owl:ObjectProperty),
	(?X ?P ?A),(?A rdf:type ?D ) -> (?X rdf:type ?R )]

[someRec2b: 
	(?R owl:someValuesFrom ?D),
	(?R owl:onProperty ?P),
	(?P rdf:type owl:DatatypeProperty),
	(?X ?P ?A), isDType(?A ?D ) -> (?X rdf:type ?R )]
	
[all1: 
	(?R owl:allValuesFrom ?D),
	(?R owl:onProperty ?P),
	(?X ?P ?Y), (?X rdf:type ?R ) -> (?Y rdf:type ?D )]
	
[allRec3: 
	(?R owl:allValuesFrom ?D),
	(?R owl:onProperty ?P),
	(?P rdfs:range ?D),
	(?X rdf:type owl:NamedIndividual) -> (?X rdf:type ?R )]

[allRec4: 
	(?P rdf:type owl:FunctionalProperty),
	(?R owl:allValuesFrom ?D),
	(?R owl:onProperty ?P),
	(?X ?P ?Y), (?Y rdf:type ?D ) -> (?X rdf:type ?R )]

[allRec5: 
	(?R1 owl:onProperty ?P),
	(?R1 owl:maxCardinality 1),
	(?R2 owl:allValuesFrom ?D),
	(?R2 owl:onProperty ?P),
	(?X ?P ?Y), (?Y rdf:type ?D ) -> (?X rdf:type ?R1 )]

